# Stores global and Puppet environment keys to the backends (key/value stores)
#
# - Standard set of test keys to store are specified by
#   $kv_test::params::key_info and $kv_test::params::binary_key_info.
# - The metadata Hash for each key is generated by this class.
#   - Single 'id' String element.
#   - 'id' includes the name of the backend, either 'global' or the
#      Puppet environment, and the key.
#
class kv_test::store inherits kv_test::params
{

  $kv_test::params::key_info.each |String $backend, Kv_test::KeyInfo $key_info| {
    kv_test::store_keys($key_info, $backend)
  }

  $kv_test::params::binary_key_info.each |String $backend, Kv_test::KeyInfo $key_info| {
    # Tranform each binary file into a Binary type
    if 'keys' in $key_info {
      $_binary_key_pairs = $key_info['keys'].map |$key,$path| {
        [ $key, binary_file($path) ]
      }
    }
    else {
      $_binary_key_pairs = []
    }

    if 'global_keys' in $key_info {
      $_global_binary_key_pairs = $key_info['global_keys'].map |$key,$path| {
        [ $key, binary_file($path) ]
      }
    }
    else {
      $_global_binary_key_pairs = []
    }

    $_binary_key_info = {
      'keys'        => Hash($_binary_key_pairs),
      'global_keys' => Hash($_global_binary_key_pairs)
    }

    kv_test::store_keys($_binary_key_info, $backend)
  }

}
